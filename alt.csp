include "channel.csp"

-- The fields for PJAlt - i and selected.  These are used in the enable/disable sequence.
-- As only one PJAlt exists we do not require a further indentifier.
channel i, selected : Operations.Channels

ALT_VARIABLES = 
    VARIABLE(selected, NONE)
    ||| VARIABLE(i, NONE)

alphaALT_VARIABLES = 
{
    selected.o.c,
    i.o.c
    | o <- Operations, c <- Channels
}

-- ALT
ENABLE_LOOP(pid, <>) = 
    i.set!NONE ->
    SKIP

ENABLE_LOOP(pid, <next>^guards) = 
    channel_claim.next.pid ->
    writer.next.get?w ->
    if (w != NULL) then
        i.set!next ->
        channel_release.next!pid ->
        ready.pid.set!true ->
        SKIP
    else
        reader.next.set!pid ->
        channel_release.next!pid ->
        ENABLE_LOOP(pid, guards)

ENABLE(pid) =
    ENABLE_LOOP(pid, seq(Active_Channels))

alphaENABLE(pid) = 
{
    writer.c.get.np,
    ready.pid.set.true,
    reader.c.set.pid,
    channel_claim.c.pid,
    channel_release.c.pid
    | c <- Active_Channels, np <- Nullable_Processes
}

DISABLE_LOOP(pid, <>) = SKIP

DISABLE_LOOP(pid, <next>^procs) =
    channel_claim.next.pid ->
    writer.next.get?w ->
    reader.next.set!NULL ->
    channel_release.next!pid ->
    (
        if (w != NULL) then
            selected.set!next ->
            SKIP
        else
            SKIP
    );
    DISABLE_LOOP(pid, procs)

reverse :: (Set a) => (<a>) -> <a>
reverse(<>) = <>
reverse(<x>^xs) = reverse(xs)^<x>

build_disable_list(<>, y) = <>
build_disable_list(<x>^xs, y) =
    if x != y then <x>^build_disable_list(xs, y)
    else <x>

DISABLE(pid) =
    i.get?end ->
    DISABLE_LOOP(pid, reverse(build_disable_list(seq(Active_Channels), end)))

alphaDISABLE(pid) =
{
    writer.c.get.np,
    reader.c.set.NULL,
    selected.set.c,
    channel_claim.c.pid,
    channel_release.c.pid
    | c <- Active_Channels, np <- Nullable_Processes
}